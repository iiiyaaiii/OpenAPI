*** Settings ***
Suite Setup       ManageProfile.Go To Change Password Page    ${buyer_account_change_pwd}    ${buyer_account_change_pwd_old}
Suite Teardown    Run Keywords    Clear Active Login    AND    Close All Browsers
Test Teardown     Capture Page Screenshot
Force Tags        Account-Web    ManageProfile    Ligo    mc-wls
Test Timeout      3 minutes
Library           SeleniumLibrary    15    5    run_on_failure=Capture Page Screenshot
Library           String
Resource          ../../../../Resource/WeLoveShopping/init.robot
Resource          ../../../../Resource/WeLoveShopping/WebElement/Member_SignIn.txt
Resource          ../../../../Resource/WeLoveShopping/WebElement/Widget.txt
Resource          ../../../../Resource/WeLoveShopping/WebElement/ManageProfile.txt
Resource          ../../../../Keyword/Common/Member_SignIn.txt
Resource          ../../../../Keyword/Common/Common.txt
Resource          ../../../../Keyword/Common/ManageProfile.txt
Resource          ../../../../Keyword/Common/Member_Register.txt

*** Test Cases ***
TC_MC_00052
    [Documentation]    [Welove]Change password : Invalid Format
    [Tags]    Regression    Sprint2017MC5    Medium
    [Template]    ManageProfile.Change Password Validation
    ${buyer_account_change_pwd_old}      12345678           12345678            Invalid Format
    ${buyer_account_change_pwd_old}      testbuyer          testbuyer           Invalid Format
    ${buyer_account_change_pwd_old}      TESTBUYER4         TESTBUYER4          Invalid Format
    ${buyer_account_change_pwd_old}      testbuyer1         testbuyer1          Invalid Format
    ${buyer_account_change_pwd_old}      TESTBUYER9         TESTBUYER9          Invalid Format
    ${buyer_account_change_pwd_old}      TEST@buyer         TEST@buyer          Invalid Format
    ${buyer_account_change_pwd_old}      123!4567           123!4567            Invalid Format

TC_MC_00054
    [Documentation]    [Welove]Change password : Invalid Range
    [Tags]    Regression    Sprint2017MC5    Medium
    [Template]    ManageProfile.Change Password Validation
    ${buyer_account_change_pwd_old}      12345              12345               Invalid Range
    ${buyer_account_change_pwd_old}      P@ss01             P@ss01              Invalid Range

TC_MC_00068
    [Documentation]    [Welove]Change password : Invalid Data Type
    [Tags]    Regression    Sprint2017MC5    Medium
    [Template]    ManageProfile.Change Password Validation
    ${buyer_account_change_pwd_old}      TestBuyer1เทส      ${EMPTY}       Invalid Data Type

TC_MC_00072
    [Documentation]    [Welove]Change password
    [Tags]    Regression    SmokeTest    Sprint2017MC5    High
    ManageProfile.Enter Current Password    ${buyer_account_change_pwd_old}
    ManageProfile.Enter New Password    ${buyer_account_change_pwd_new}
    ManageProfile.Enter New Password Confirmation    ${buyer_account_change_pwd_new}
    ManageProfile.Click Change Password Button
    ManageProfile.Successfully Changed Password Page Should Be Appeared
    [Teardown]    Reset Password To Default

*** Keywords ***
Reset Password To Default
    ManageProfile.Enter Current Password    ${buyer_account_change_pwd_new}
    ManageProfile.Enter New Password    ${buyer_account_change_pwd_old}
    ManageProfile.Enter New Password Confirmation    ${buyer_account_change_pwd_old}
    ManageProfile.Click Change Password Button
    ManageProfile.Successfully Changed Password Page Should Be Appeared
