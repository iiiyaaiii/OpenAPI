*** Settings ***
Suite Setup       Open Custom Browser    ${URL_PORTAL}    ${BROWSER}
Suite Teardown    Close All Browsers
Test Teardown     Run Keywords    Capture Page Screenshot    AND    Delete All Product In Cart
Force Tags        Account-Web    mc-wls
Test Timeout      3 minutes
Library           String
Library           SeleniumLibrary    15    5    run_on_failure=Capture Page Screenshot
Resource          ../../../../Resource/WeLoveShopping/init.robot
Resource          ../../../../Resource/WeLoveShopping/WebElement/Member_SignIn.txt
Resource          ../../../../Resource/WeLoveShopping/WebElement/Widget.txt
Resource          ../../../../Resource/WeLoveShopping/WebElement/Checkout.txt
Resource          ../../../../Keyword/Common/Member_SignIn.txt
Resource          ../../../../Keyword/Common/Common.txt
Resource          ../../../../Keyword/Common/Checkout.txt

*** Test Cases ***
TC_MC_00002
    [Documentation]    Checkout with already logon
    [Tags]    Regression    High    mock
    [Setup]    Go to Login    ${buyer_checkout}    ${buyer_checkout_pass}
    Add Product To Cart And Go To Checkout Page
    Checkout Step1 Is Ticked
    [Teardown]    Run Keywords    Capture Page Screenshot    AND    Clear Active Login

TC_MC_00004
    [Documentation]    Checkout without logon
    [Tags]    Regression    High    mock
    Add Product To Cart And Go To Checkout Page
    Checkout Step1 Is Unticked
    Login During Checkout Step    ${buyer_checkout}    ${buyer_checkout_pass}
    Checkout Step1 Is Ticked

*** Keyword ***
Add Product To Cart And Go To Checkout Page
    Go to    ${URL_LEVEL_D}/${robot_store1_instock_type_use}&refresh=changeme
    Checkout.Add Product To Cart And Go Checkout
    Checkout.Your Cart Page Should Be Appeared
    Checkout.Checkout On The Selected Shop    ${store_name1}

Checkout Step1 Is Ticked
    Wait Until Element Is Visible    ${step1_complete}

Checkout Step1 Is Unticked
    Element Should Not Be Visible    ${step1_complete}

Login During Checkout Step
    [Arguments]    ${username}    ${password}
    Input Text    //input[@id='username']    ${username}
    Input Text    //input[@id='password']    ${password}
    Click Element    ${btn_login}

